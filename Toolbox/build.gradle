plugins {
    id 'java'
    id 'maven-publish'
    id 'java-library'
    id 'signing'
}

group = 'org.reldb'
version = '1.0.0'
description = 'RelDB Toolbox'
java.sourceCompatibility = JavaVersion.VERSION_11

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
//    sign configurations.archives
//    sign javadocJar
//    sign sourcesJar
    sign publishing.publications
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.0'
    testImplementation 'org.junit.platform:junit-platform-runner:1.8.0'
}

test {
    useJUnitPlatform()
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/toolbox.version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "RelDB Toolbox"
                description = "Miscellaneous tools used by various RelDB.org projects."
                url = "https://github.com/DaveVoorhis/Toolbox"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "DaveVoorhis"
                        name = "Dave Voorhis"
                        email = "voorhis@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/DaveVoorhis/Toolbox.git"
                    developerConnection = "scm:git:https://github.com/DaveVoorhis/Toolbox.git"
                    url = "https://github.com/DaveVoorhis/Toolbox"
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

javadoc {
    doLast {
        delete "docs/javadoc"
        copy {
            from "build/docs/"
            into "docs/"
        }
    }
}
